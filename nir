#!/usr/bin/env perl

##
## Mr Netops's nifty inline resolver
## Copyright 2013 Todd Jimenez - Todd.Jimenez@gmail.com
## https://github.com/mrnetops/nir
##

##
## This program is free software: you can redistribute it and/or modify
## it under the terms of the GNU General Public License version 2 
## as published by the Free Software Foundation
##
## This program is distributed in the hope that it will be useful,
## but WITHOUT ANY WARRANTY; without even the implied warranty of
## MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
## GNU General Public License for more details.
##
## You should have received a copy of the GNU General Public License
## along with this program.  If not, see 
## <http://www.gnu.org/licenses/gpl-2.0.html>.
##

use Socket qw(:all);
use Getopt::Long;
use Data::Dumper;
use strict;
use warnings;

##
## nasty way to address ipv6 Socket constants not being present in older
## Socket libraries
##

no strict "subs";

%::commandLineOptions = (

##
## Per http://stackoverflow.com/questions/106179/regular-expression-to-match-hostname-or-ip-address
##
## with some regexp bug fixes
## 	* reversed the ip address octet subregexp to ensure we do a greedy
##	  match on the last octet otherwise we only match up to the first digit
##	  of the last octet.
##	* added + to the [A-Za-z] hostname subregexp to ensure we do greedy
##	  matching on the top level domain otherwise we only match up to the
##	  first letter of the top level domain.
##	* escape "." to prevent it doing an any character match instead of a
##	  literal "." match
##

	'ipV4AddressRegexp' =>
		'((25[0-5]|2[0-4][0-9]|1[0-9]{2}|[1-9][0-9]|[0-9])\.){3}(25[0-5]|2[0-4][0-9]|1[0-9]{2}|[1-9][0-9]|[0-9])',

##
## It's suprising difficult to find a good ipv6 regexp
## Massaged from Regexp::IPv6
##

	'ipV6AddressRegexp' =>
		'(?^::(:[0-9a-fA-F]{1,4}){0,5}((:[0-9a-fA-F]{1,4}){1,2}|:((25[0-5]|2[0-4][0-9]|[0-1]?[0-9]{1,2})[.](25[0-5]|2[0-4][0-9]|[0-1]?[0-9]{1,2})[.](25[0-5]|2[0-4][0-9]|[0-1]?[0-9]{1,2})[.](25[0-5]|2[0-4][0-9]|[0-1]?[0-9]{1,2})))|[0-9a-fA-F]{1,4}:([0-9a-fA-F]{1,4}:([0-9a-fA-F]{1,4}:([0-9a-fA-F]{1,4}:([0-9a-fA-F]{1,4}:([0-9a-fA-F]{1,4}:([0-9a-fA-F]{1,4}:([0-9a-fA-F]{1,4}|:)|(:([0-9a-fA-F]{1,4})?|((25[0-5]|2[0-4][0-9]|[0-1]?[0-9]{1,2})[.](25[0-5]|2[0-4][0-9]|[0-1]?[0-9]{1,2})[.](25[0-5]|2[0-4][0-9]|[0-1]?[0-9]{1,2})[.](25[0-5]|2[0-4][0-9]|[0-1]?[0-9]{1,2}))))|:(((25[0-5]|2[0-4][0-9]|[0-1]?[0-9]{1,2})[.](25[0-5]|2[0-4][0-9]|[0-1]?[0-9]{1,2})[.](25[0-5]|2[0-4][0-9]|[0-1]?[0-9]{1,2})[.](25[0-5]|2[0-4][0-9]|[0-1]?[0-9]{1,2}))|[0-9a-fA-F]{1,4}(:[0-9a-fA-F]{1,4})?|))|(:((25[0-5]|2[0-4][0-9]|[0-1]?[0-9]{1,2})[.](25[0-5]|2[0-4][0-9]|[0-1]?[0-9]{1,2})[.](25[0-5]|2[0-4][0-9]|[0-1]?[0-9]{1,2})[.](25[0-5]|2[0-4][0-9]|[0-1]?[0-9]{1,2}))|:[0-9a-fA-F]{1,4}(:((25[0-5]|2[0-4][0-9]|[0-1]?[0-9]{1,2})[.](25[0-5]|2[0-4][0-9]|[0-1]?[0-9]{1,2})[.](25[0-5]|2[0-4][0-9]|[0-1]?[0-9]{1,2})[.](25[0-5]|2[0-4][0-9]|[0-1]?[0-9]{1,2}))|(:[0-9a-fA-F]{1,4}){0,2})|:))|((:[0-9a-fA-F]{1,4}){0,2}(:((25[0-5]|2[0-4][0-9]|[0-1]?[0-9]{1,2})[.](25[0-5]|2[0-4][0-9]|[0-1]?[0-9]{1,2})[.](25[0-5]|2[0-4][0-9]|[0-1]?[0-9]{1,2})[.](25[0-5]|2[0-4][0-9]|[0-1]?[0-9]{1,2}))|(:[0-9a-fA-F]{1,4}){1,2})|:))|((:[0-9a-fA-F]{1,4}){0,3}(:((25[0-5]|2[0-4][0-9]|[0-1]?[0-9]{1,2})[.](25[0-5]|2[0-4][0-9]|[0-1]?[0-9]{1,2})[.](25[0-5]|2[0-4][0-9]|[0-1]?[0-9]{1,2})[.](25[0-5]|2[0-4][0-9]|[0-1]?[0-9]{1,2}))|(:[0-9a-fA-F]{1,4}){1,2})|:))|((:[0-9a-fA-F]{1,4}){0,4}(:((25[0-5]|2[0-4][0-9]|[0-1]?[0-9]{1,2})[.](25[0-5]|2[0-4][0-9]|[0-1]?[0-9]{1,2})[.](25[0-5]|2[0-4][0-9]|[0-1]?[0-9]{1,2})[.](25[0-5]|2[0-4][0-9]|[0-1]?[0-9]{1,2}))|(:[0-9a-fA-F]{1,4}){1,2})|:)))',

	'hostnameRegexp' =>
		"(([a-zA-Z]|[a-zA-Z][a-zA-Z0-9\-]*[a-zA-Z0-9])\\.)+([A-Za-z]+|[A-Za-z][A-Za-z0-9\-]*[A-Za-z0-9])",
		#"(([a-zA-Z]|[a-zA-Z][a-zA-Z0-9\-]*[a-zA-Z0-9])\\.)*([A-Za-z]+|[A-Za-z][A-Za-z0-9\-]*[A-Za-z0-9])",
	'format' =>
		'[%s]',
	'separator' =>
		',',
	'tags' =>
		0,
	'timeout' =>
		5,
);

##
## Perl 5.14.0 or newer is required for IPv6 support
## Unless Socket is manually updated
##

eval {
	require 5.14.0;
};

if ($@) { 
	$::commandLineOptions{ipV6Support} = 0;
} else {
	$::commandLineOptions{ipV6Support} = 1;
}

@::getOptionsOptions = (
	\%::commandLineOptions,
	'ipV4AddressRegexp=s',
	'hostnameRegexp=s',
	'ipV6Support!',
	'format=s',
	'separator=s',
	'tags!',
	'verbose!',
	'debug!',
	'timeout=i',
	'lookup=s@',
	'help|h|?',
);

if (exists $ENV{'NIR'}) {
    @ARGV = (
        split(' ', $ENV{NIR}), @ARGV
    );
}

GetOptions(
        @::getOptionsOptions
) or exit 1;

##
## GetOptions arrays are additive so set $::commandLineOptions{lookup}
## conditionally AFTER calling GetOptions, otherwise we could never use
## a subset of the default, only a superset.
##

unless (exists $::commandLineOptions{lookup}) {
	$::commandLineOptions{lookup} = [
		'hostname', 'ipv4', 'ipv6', 'canonname'
	],
}

$::commandLineOptions{lookup} = [
	split(
		/,/, 
		join(
			',', 
			@{ $::commandLineOptions{lookup} }
		)
	)
];

if (exists $::commandLineOptions{help}) {
    print "
Mr. Netops's nifty inline resolver

Options:

	-format <format>
		Specify alternate formatting for resolved values
	-separator <separator>
		Specify alternate separator for lookups with multiple values
	-tags
		Add type tags to resolved values
	-verbose
		Print additional status information to stderr
	-debug
		Print debugging information to stderr
	-timeout <timeout>
		Dns lookup timeout
	-lookup <types>
		Types of resolved values and their presentation order
		Supports The following types: hostname, ipv4, ipv6, canonname
	-help
		This

Advanced Options:

	-hostnameRegexp <regexp>
		Specify an alternate hostname regular expression
	-ipV4AddressRegexp <regexp>
		Specify an alternate ipv4 address regular expression
	-ipV6AddressRegexp <regexp>
		Specify an alternate ipv6 address regular expression
	-ipV6Support
		Manually enable ipv6 support
	-noipV6Support
		Manually disable ipv6 support

Examples:

	Basic invocation
		someCommand | $0

	Present ipv4 & ipv6 resolved values in that order
		someCommand | $0 -lookup ipv4,ipv6

	Format inline lookups in curly braces and seperate values with ;
		someCommand | $0 -format '{%s}' -separator ';'

Use Cases:
			
	dig facebook.com ANY | $0
	mtr -wr facebook.com | $0
	cat /etc/hosts | $0
	arp -a | $0
	
";
	exit 0;
}
	
if (exists $::commandLineOptions{debug}) {
	$Data::Dumper::Varname = 'Configuration';
	$Data::Dumper::Sortkeys = 1;
	&debug(
		Dumper(\%::commandLineOptions)
	);
}

if ($::commandLineOptions{ipV6Support}) {
	$::masterRegexp = join(
		"|",
		$::commandLineOptions{'ipV4AddressRegexp'},
		$::commandLineOptions{'ipV6AddressRegexp'},
		$::commandLineOptions{'hostnameRegexp'}
	);
} else {
	$::masterRegexp = join(
		"|",
		$::commandLineOptions{'ipV4AddressRegexp'},
		$::commandLineOptions{'hostnameRegexp'}
	);
}

while (<>) {
	$_ =~ s/($::masterRegexp)/&resolveMe($1)/ge;
	print $_;
}

sub resolveMe {
	my ($string) = @_;

	if (exists $::cache{$string}) {
		return $::cache{$string}{'formatted'};
	}

	if ($string =~ m/$::commandLineOptions{'hostnameRegexp'}/) {
		&resolveMe_hostname($string);
	} else {
		&resolveMe_ipAddress($string);
	}

	&format($string);
	return $::cache{$string}{'formatted'};
}

sub resolveMe_hostname {
	if ($::commandLineOptions{ipV6Support}) {
		&resolveMe_getStarInfo('hostname', @_);
	} else {
		&resolveMe_gethostbyname(@_);
	}
}

sub resolveMe_ipAddress {
	if ($::commandLineOptions{ipV6Support}) {
		&resolveMe_getStarInfo("ip", @_);
	} else {
		&resolveMe_gethostbyaddr(@_);
	}
}

sub resolveMe_gethostbyname {
	my ($string) = @_;

	&debug("gethostbyname [$string]");

	my @addresses = gethostbyname($string);
	if (@addresses) {
		@addresses = map {
			inet_ntoa($_)
		} @addresses[4 .. $#addresses];

		if ($addresses[0] ne $string) {
			foreach my $address (@addresses) {
				++$::cache{
					$string
				}{
					'resolved'
				}{
					'ipv4'
				}{
					$address
				};
			}
		}
	}	
}

sub resolveMe_gethostbyaddr {
	my ($string) = @_;

	&debug("gethostbyaddr [$string]");

	my $name;

	eval {
		alarm(0);
		local $SIG{ALRM} = sub { die; };
		alarm($::commandLineOptions{'timeout'});

		my $inet_aton = inet_aton($string);

		if (defined $inet_aton) {
			$name = gethostbyaddr($inet_aton, AF_INET);
		}

		alarm(0);
	};

	if ($@) { 

		&verbose("gethostbyaddr timeout [$string]");

		return $::cache{$string}; 
	}

	if (defined $name) {
		++$::cache{$string}{'resolved'}{'hostname'}{$name};
	} 
}

##
## getaddrinfo / getnameinfo lookups
##

sub resolveMe_getStarInfo {
	my ($type, $string) = @_;

	my %hints = (
		flags => AI_CANONNAME,
	);

	&debug("getaddrinfo [$string]");

	my ($error, @addresses) = getaddrinfo(
		$string, 
		undef,
		\%hints
	);

	my %lookup;
	my %information;
	my $flags = $type eq 'hostname' ? NI_NUMERICHOST : 0;

	foreach my $address (@addresses) {

		if (exists $lookup{ $address->{addr} }) { next; }
		++$lookup{ $address->{addr} };

		if (
			$address->{canonname}
		) {
			++$::cache{$string}{'resolved'}{'canonname'}{
				$address->{canonname}
			};
		}

		my ($err, $hostname);
		eval {
			alarm(0);
			local $SIG{ALRM} = sub { die; };
			alarm($::commandLineOptions{'timeout'});

			($err, $hostname) = getnameinfo(
				$address->{addr}, $flags
			);

			alarm(0);
		};

		if ($@ || $err) { 
			&verbose("getnameinfo error [$string]: [$@$err]");
		} else {
			if ($type eq 'hostname') {
				if ($address->{family} == AF_INET) {
					++$::cache{
						$string
					}{
						'resolved'
					}{
						'ipv4'
					}{
						$hostname
					};
				}
				if ($address->{family} == AF_INET6) {
					++$::cache{
						$string
					}{
						'resolved'
					}{
						'ipv6'
					}{
						$hostname
					};
				}
			} else {
				++$::cache{
					$string
				}{
					'resolved'
				}{
					'hostname'
				}{
					$hostname
				};
			}
		}
	}
}

sub format {
	my ($string) = @_;

	unless(exists $::cache{$string}{'resolved'}) {
		$::cache{$string}{'formatted'} = $string;
		return;
	}

	my @elements;
	foreach my $type (
		@{ $::commandLineOptions{'lookup'} }
	) {
		unless (exists $::cache{$string}{'resolved'}{$type}) { next; }
		foreach my $address (
			keys %{
				$::cache{$string}{'resolved'}{$type}
			}
		) {
			if ($string eq $address) { next; }
			if ($::commandLineOptions{'tags'}) {
				push(@elements, "${type}:${address}");
			} else {
				push(@elements, $address);
			}
		}
	}

	if (@elements) {
		$::cache{$string}{'formatted'} = $string . sprintf(
			$::commandLineOptions{'format'},
			join($::commandLineOptions{'separator'}, @elements)
		);
	} else {
		$::cache{$string}{'formatted'} = $string;
	}
}

sub verbose {
	my ($message) = @_;

	if (
		exists $::commandLineOptions{'verbose'}
	) {
		print STDERR "VERBOSE: $message\n";
	}
}

sub debug {
	my ($message) = @_;

	if (
		exists $::commandLineOptions{'debug'}
	) {
		print STDERR "DEBUG: $message\n";
	}
}
